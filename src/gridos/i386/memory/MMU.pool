/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace gridos::i386::memory;

use sys::core::Object;
use sys::core::utils::Bitset;
use sys::memory::Address;
use sys::memory::PageAllocator;

class MMU extends Object {
    int activeDirectory;
    PhysicalFreePageInfo info;
    
    [] __init() {
        int cr0 = 0;
        __pasm__("movl %cr0, %eax", {}, {"%eax": cr0});
        if (Bitset:has(cr0, 0x80000001)) {
            this.refreshActiveDirectory();
        } else {
            activeDirectory = 0;
        }
        info = Address:cast(0xFFBFB000);
    }
    
    [int] isActive() { return this.activeDirectory != 0; }
    [int] getActiveDirectory() { return this.activeDirectory; }
    [] refreshActiveDirectory() {
        __pasm__("movl %cr3, %eax", {}, {"%eax": this.activeDirectory});
    }

    [] activate(int pageDirectoryPAddr) {
        if (pageDirectoryPAddr == 0) {
            this.rt().err().printCString("invalid page directory to activate");
            this.rt().out().printNewline();
            return;
        }
        __pasm__(<"
             movl %eax, %cr3 
             movl %cr0, %eax
             orl 0x80000001, %eax 
             movl %eax, %cr0
        ">, {"%eax": pageDirectoryPAddr});
        this.activeDirectory = pageDirectoryPAddr;
    }
    
    [] flushTLB() {
        __pasm__(<"
             movl %cr3, %eax 
             movl %eax, %cr3
        ">, {}, {"%eax": null}); 
    }
    
    [] map(int pAddr, int vAddr) {
        int pdIndex = Bitset:rshift(vAddr, 22);
        int ptIndex = Bitset:and(Bitset:rshift(vAddr, 12), 0x3ff);
        int pdEntryAddr = 0xFFFFF000 + 4*pdIndex;
        int ptEntryAddr = 0xFFC00000 + Bitset:lshift(pdIndex, 12) + 4*ptIndex;
        
        int pdEntry = Address:loadAddress(pdEntryAddr);
        if (!Bitset:has(pdEntry, 0x1)) {
            Address:storeAddress(pdEntryAddr, this.newPageTable() + 0x1);
            this.flushTLB();
        }
        Address:storeAddress(ptEntryAddr, Bitset:and(pAddr, 0xfffff000) + 0x1); 
    }
    
    [int] unmap(int vAddr) {
        int pdIndex = Bitset:rshift(vAddr, 22);
        int ptIndex = Bitset:and(Bitset:rshift(vAddr, 12), 0x3ff);
        int pdEntryAddr = 0xFFFFF000 + 4*pdIndex;
        int ptEntryAddr = 0xFFC00000 + Bitset:lshift(pdIndex, 12) + 4*ptIndex;
        
        int pdEntry = Address:loadAddress(pdEntryAddr);
        if (!Bitset:has(pdEntry, 0x1)) {
            return -1;
        }
        int ptEntry = Address:loadAddress(ptEntryAddr);
        if (!Bitset:has(ptEntry, 0x1)) {
            return -1;
        }
        Address:storeAddress(ptEntryAddr, 0); 
        return Bitset:and(ptEntry, 0xfffff000); 
    }
    
    [] mapPages(int pAddr, int vAddr, int pages) {
        int curP = pAddr;
        int curV = vAddr;
        int c = 0;
        while (c++ < pages) {
            this.map(curP, curV);
            curP += PageAllocator:PAGESIZE;
            curV += PageAllocator:PAGESIZE;
        }
    }
    
    [int] unmapPages(int vAddr, int pages) {
        int ret = this.unmap(vAddr);
        int curV = vAddr + PageAllocator:PAGESIZE;
        int c = 1;
        while (c++ < pages) {
            this.unmap(curV);
            curV += PageAllocator:PAGESIZE;
        }
        return ret;
    }
    
    [int] getPPage(int vAddr) {
        int pdIndex = Bitset:rshift(vAddr, 22);
        int ptIndex = Bitset:and(Bitset:rshift(vAddr, 12), 0x3ff);
        int pdEntryAddr = 0xFFFFF000 + 4*pdIndex;
        int ptEntryAddr = 0xFFC00000 + Bitset:lshift(pdIndex, 12) + 4*ptIndex;
        
        int pdEntry = Address:loadAddress(pdEntryAddr);
        if (!Bitset:has(pdEntry, 0x1)) {
            return -1;
        }
        int ptEntry = Address:loadAddress(ptEntryAddr);
        if (!Bitset:has(ptEntry, 0x1)) {
            return -1;
        }
        return Bitset:and(ptEntry, 0xfffff000); 
    }
    
    [int] allocatePagesAt(int virtualAddress, int count) {
        int pages = count;
        int allocated = 0;
        if (info.count >= pages) {
            info.count -= pages;
            int pAddr = Address:offsetAddress(info.self, info.count * PageAllocator:PAGESIZE);
            int vAddr = virtualAddress;
            while (pages-- > 0 && (this.getPPage(vAddr) == -1)) {
                this.map(pAddr, vAddr);
                pAddr += PageAllocator:PAGESIZE;
                vAddr += PageAllocator:PAGESIZE;
                allocated++;
            }
        }
        this.flushTLB();
        return allocated;
        // TODO: handle page info list
    }
    
    [] freePagesAt(int virtualAddress, int count) {
        // TODO: mark pages as free and merge into free page info list
        int pages = count;
        int cur = virtualAddress;
        while (pages-- > 0) {
            this.unmap(cur);
            cur += PageAllocator:PAGESIZE;
        }
    }
    
    [int] newPageTable() {
        this.allocatePagesAt(0xFFBF9000, 1);
        // clear new page table
        Address:setBytes(0xFFBF9000, PageAllocator:PAGESIZE, 0);
        return this.unmap(0xFFBF9000);
    }
    
    [int, StackBuilder] newThreadPageDirectoryAndKernelStack(int topOfStack) {
        // allocate thread kernel stack page, new page directory, and page tables for userspace and stack page 
        this.allocatePagesAt(0xFFBF0000, 4);
        int pd = this.getPPage(0xFFBF0000);
        int zeroPt = this.getPPage(0xFFBF1000);
        int stackPt = this.getPPage(0xFFBF2000);
        int stack = this.getPPage(0xFFBF3000);

        int vStackPage = topOfStack - PageAllocator:PAGESIZE;
        int stackPdIndex = Bitset:rshift(vStackPage, 22);
        int stackPtIndex = Bitset:and(Bitset:rshift(vStackPage, 12), 0x3ff);
        
        // setup page tables
        Address:setBytes(0xFFBF1000, PageAllocator:PAGESIZE, 0);
        Address:setBytes(0xFFBF2000, PageAllocator:PAGESIZE, 0);
        // add stack page
        Address:storeAddress(0xFFBF2000 + 4*stackPtIndex, stack + 0x1);
        
        // setup page directory
        // clear user space mapping
        Address:setBytes(0xFFBF0000, 4*0x3C0, 0);
        // keep kernel space mapping
        Address:copyBytes(0xFFFFF000 + 4*0x3C0, 0xFFBF0000 + 4*0x3C0, 4*0x40);
        // add new page tables
        Address:storeAddress(0xFFBF0000 + 4*0x0, zeroPt + 0x1);
        Address:storeAddress(0xFFBF0000 + 4*stackPdIndex, stackPt + 0x1);
        Address:storeAddress(0xFFBF0000 + 4*0x3FF, pd + 0x1);
        
        // unmap page directory and tables
        this.unmap(0xFFBF0000);
        this.unmap(0xFFBF1000);
        this.unmap(0xFFBF2000);
        
        // keep kernel stack mapped and return as StackBuilder to be filled by caller
        StackBuilder sb = this.rt().createInstance(StackBuilder:CLASSNAME);
        sb.init(0xFFBF3000, 1, vStackPage - 0xFFBF3000);
        return [pd, sb];
    }
    
}

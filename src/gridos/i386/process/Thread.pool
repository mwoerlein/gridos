/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace gridos::i386::process;

use gridos::i386::sys::Thread as UserThread;

use sys::runtime::Process as SysProcess;
use sys::runtime::Thread as SysThread;
use sys::time::Timestamp;

class Thread extends sys::core::Object {

    Process process;
    UserThread userThread;
    
    Timestamp cpuTS;
    int topOfStack;
    Thread next;
    
    [Thread] _initStart(Process p) {
        this.process = p;
        return this;
    }
    
    [Thread] _initFinish() {
        sys::runtime::Runtime urt = this.process.getUserRuntime();
        this.cpuTS = this.own(urt.createInstance(Timestamp:CLASSNAME));
        this.cpuTS.setTime(0, 0);
        this.userThread = this.own(urt.createInstance(UserThread:CLASSNAME));
        this.userThread._initThread(this.process.getUserProcess(), this.cpuTS);
        return this;
    }
    
    [Process] getKernelProcess() { return this.process; }
    [UserThread] getUserThread() { return this.userThread; }
    
    [int, int] getCPUTime() { [int s, int n] := cpuTS.getTime(); return [s, n]; }
    [] addCPUTime(int sec, int nsec) {
        cpuTS.addTime(sec, nsec);
        process.addCPUTime(sec, nsec);
    }
    
    [int] getPid() { return this.process.getPid(); }
    [int] getPageDirectoryPAddr() { return this.process.getPageDirectoryPAddr(); }
    
    [int] getStatus() {
        // TODO #24
        return SysThread:STATUS_READY;
    }
    [] awaitTermination() {
        // TODO #24
    }
    
    [Thread] setTopOfStack(int tos) { this.topOfStack = tos; return this; }
    [int] getTopOfStack() { return this.topOfStack; }
    
    [Thread] setNext(Thread n) { this.next = n; return this; }
    [Thread] getNext() { return this.next; }
    
}

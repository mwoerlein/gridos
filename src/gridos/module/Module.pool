/*[meta]
mimetype = text/x-pool
author = Marc Woerlein <marc.woerlein@gmx.de>
version = 0.1.0
*/
namespace gridos::module;

use sys::core::String;
use sys::core::anycollection::AnyIterator;
use sys::core::anycollection::AnyList;
use sys::core::utils::CommandLineParser;
use sys::core::utils::CommandLineParserTarget;
use sys::memory::MemoryIOStream;
use sys::runtime::Runtime;
use sys::store::StorageElement;
use sys::stream::IStream;
use sys::stream::OStream;

class Module extends StorageElement, CommandLineParserTarget {

    cstring PROPERTYPREFIX_DEFAULT := "meta.";
    cstring PROPERTYNAME_MODULE_ID := "meta.id";
        
    int _start;
    int _contentStart;
    int _end;
    
    [] init(int startAddr, int endAddr) {
        _start = _contentStart = startAddr;
        _end = endAddr;
        this._initStorageElement();
    }
    [] __destruct() {
        this._destructStorageElement();
    }
    
    [int] hasId() {
        return this.hasCStringProperty(PROPERTYNAME_MODULE_ID);
    }
    
    [String] getId() {
        return this.getCStringProperty(PROPERTYNAME_MODULE_ID);
    }
    
    [] setId(String id) {
        String tmp = this.createStringFromCString(PROPERTYNAME_MODULE_ID); 
        this.setStringProperty(tmp, id);
        tmp.destroy();
    }

    [IStream] getContent() {
        MemoryIOStream s = this.rt().createInstance(MemoryIOStream:CLASSNAME);
        s._initBuf(_contentStart, _end - _contentStart);
        return s.istream();
    }
    
    [int] getContentSize() {
        return _end - _contentStart;
    }
    
    [int] parseCommandline(cstring commandline) {
        Runtime rt = this.rt();
        CommandLineParser p = rt.createInstance(CommandLineParser:CLASSNAME);
        AnyList args = p.split(commandline);
        AnyIterator ai = args.iterator();
        int ret = true;
        if (ai.hasNext()) {
            this.setId(this.createOwnStringFromString(ai.next()));
            ret = p.parse(ai, rt.cast(CommandLineParserTarget:CLASSNAME, this));
        }
        ai.destroy();
        args.destroyAll();
        args.destroy();
        p.destroy();
        return ret;
    }
    
    [int] pushCLArgument(String arg) {
        this.rt().err().printCString("modules do not support arguments! given: ").print(arg).printNewline();
        return false;
    }
    
    [int] pushCLOption(String opt) {
        this.rt().err().printCString("modules do not support options! given: ").print(opt).printNewline();
        return false;
    }
    
    [int] pushCLValueOption(String opt, String val) {
        String name = this.createStringFromCString(PROPERTYPREFIX_DEFAULT);
        name.print(opt);
        String old = this.setStringProperty(name, this.createOwnStringFromString(val));
        if (old) {
            old.destroy();
        }
        name.destroy();
        return true;
    }
    
    [OStream] dumpToStream(OStream stream, int withProperties) {
        stream.printChar('[').print(this.getId()).printChar(']').printChar(' ')
            .printChar('(').printPtr(_contentStart).printChar(':').printPtr(_end).printChar(')')
            .printNewline(); 
        if (withProperties) { this.dumpPropertiesToStream(stream); }
        return stream;
    }    
}
